# -------------------------------------------------------------------------
# NSP-specific variables
# -------------------------------------------------------------------------

# -------------------------------------------------------------------------
# AVM required resource interfaces
# -------------------------------------------------------------------------

variable "location" {
  type        = string
  description = "Azure region where the resource should be deployed."
  nullable    = false
}

variable "name" {
  type        = string
  description = "The name of the Network Security Perimeter. Must be 1-80 characters, start and end with alphanumeric, and may contain alphanumeric, underscore, period, or hyphen."

  validation {
    condition     = can(regex("(^[a-zA-Z0-9]+[a-zA-Z0-9_.\\-]*[a-zA-Z0-9]+$)|(^[a-zA-Z0-9]$)", var.name)) && length(var.name) <= 80
    error_message = "The name must be 1-80 characters long, start and end with an alphanumeric character, and may contain alphanumeric, underscore, period, or hyphen characters."
  }
}

# This is required for most resource modules
variable "resource_group_name" {
  type        = string
  description = "The resource group where the resources will be deployed."
}

variable "access_rules" {
  type = map(object({
    name                         = string
    profile_key                  = string
    direction                    = string
    address_prefixes             = optional(list(string), [])
    email_addresses              = optional(list(string), [])
    fully_qualified_domain_names = optional(list(string), [])
    phone_numbers                = optional(list(string), [])
    service_tags                 = optional(list(string), [])
    subscriptions                = optional(list(string), [])
  }))
  default     = {}
  description = <<DESCRIPTION
A map of NSP access rules to create. The map key is deliberately arbitrary to avoid issues where map keys may be unknown at plan time.

- `name` - (Required) The name of the access rule. Must follow NSP naming constraints (1-80 chars, alphanumeric with `_`, `.`, `-`).
- `profile_key` - (Required) The key from `var.profiles` that identifies the profile this rule belongs to.
- `direction` - (Required) Direction of the access rule. Possible values: `Inbound`, `Outbound`.
- `address_prefixes` - (Optional) List of inbound IPv4/IPv6 address prefixes. Applicable for `Inbound` rules.
- `email_addresses` - (Optional) List of outbound email addresses. Currently unavailable for use.
- `fully_qualified_domain_names` - (Optional) List of outbound FQDNs. Applicable for `Outbound` rules.
- `phone_numbers` - (Optional) List of outbound phone numbers. Currently unavailable for use.
- `service_tags` - (Optional) List of inbound service tag names. Currently unavailable for use.
- `subscriptions` - (Optional) List of subscription IDs (ARM format: `/subscriptions/{id}`) for cross-subscription inbound access.
DESCRIPTION
  nullable    = false

  validation {
    condition     = alltrue([for _, v in var.access_rules : contains(["Inbound", "Outbound"], v.direction)])
    error_message = "The direction must be one of: 'Inbound', 'Outbound'."
  }
}

variable "enable_telemetry" {
  type        = bool
  default     = true
  description = <<DESCRIPTION
This variable controls whether or not telemetry is enabled for the module.
For more information see <https://aka.ms/avm/telemetryinfo>.
If it is set to false, then no telemetry will be collected.
DESCRIPTION
  nullable    = false
}

variable "lock" {
  type = object({
    kind = string
    name = optional(string, null)
  })
  default     = null
  description = <<DESCRIPTION
Controls the Resource Lock configuration for this resource. The following properties can be specified:

- `kind` - (Required) The type of lock. Possible values are `\"CanNotDelete\"` and `\"ReadOnly\"`.
- `name` - (Optional) The name of the lock. If not specified, a name will be generated based on the `kind` value. Changing this forces the creation of a new resource.
DESCRIPTION

  validation {
    condition     = var.lock != null ? contains(["CanNotDelete", "ReadOnly"], var.lock.kind) : true
    error_message = "The lock level must be one of: 'None', 'CanNotDelete', or 'ReadOnly'."
  }
}

variable "profiles" {
  type = map(object({
    name = string
  }))
  default     = {}
  description = <<DESCRIPTION
A map of NSP profiles to create under the Network Security Perimeter. The map key is deliberately arbitrary to avoid issues where map keys may be unknown at plan time.

- `name` - (Required) The name of the profile. Must follow NSP naming constraints (1-80 chars, alphanumeric with `_`, `.`, `-`).
DESCRIPTION
  nullable    = false
}

variable "resource_associations" {
  type = map(object({
    name                     = string
    private_link_resource_id = string
    profile_key              = string
    access_mode              = optional(string, "Learning")
  }))
  default     = {}
  description = <<DESCRIPTION
A map of NSP resource associations to link PaaS resources to the Network Security Perimeter. The map key is deliberately arbitrary.

- `name` - (Required) The name of the resource association. Must follow NSP naming constraints.
- `private_link_resource_id` - (Required) The ARM resource ID of the PaaS resource to associate (must support Private Link).
- `profile_key` - (Required) The key from `var.profiles` that identifies the NSP profile to assign to this association.
- `access_mode` - (Optional) Access mode for the association. Possible values: `Learning`, `Enforced`, `Audit`. Defaults to `Learning`.
DESCRIPTION
  nullable    = false

  validation {
    condition     = alltrue([for _, v in var.resource_associations : contains(["Learning", "Enforced", "Audit"], v.access_mode)])
    error_message = "The access_mode must be one of: 'Learning', 'Enforced', 'Audit'."
  }
}

variable "role_assignments" {
  type = map(object({
    role_definition_id_or_name             = string
    principal_id                           = string
    description                            = optional(string, null)
    skip_service_principal_aad_check       = optional(bool, false)
    condition                              = optional(string, null)
    condition_version                      = optional(string, null)
    delegated_managed_identity_resource_id = optional(string, null)
    principal_type                         = optional(string, null)
  }))
  default     = {}
  description = <<DESCRIPTION
A map of role assignments to create on this resource. The map key is deliberately arbitrary to avoid issues where map keys maybe unknown at plan time.

- `role_definition_id_or_name` - The ID or name of the role definition to assign to the principal.
- `principal_id` - The ID of the principal to assign the role to.
- `description` - The description of the role assignment.
- `skip_service_principal_aad_check` - If set to true, skips the Azure Active Directory check for the service principal in the tenant. Defaults to false.
- `condition` - The condition which will be used to scope the role assignment.
- `condition_version` - The version of the condition syntax. Valid values are '2.0'.
- `delegated_managed_identity_resource_id` - The delegated Azure Resource Id which contains a Managed Identity. Changing this forces a new resource to be created.
- `principal_type` - The type of the principal_id. Possible values are `User`, `Group` and `ServicePrincipal`. Changing this forces a new resource to be created. It is necessary to explicitly set this attribute when creating role assignments if the principal creating the assignment is constrained by ABAC rules that filters on the PrincipalType attribute.

> Note: only set `skip_service_principal_aad_check` to true if you are assigning a role to a service principal.
DESCRIPTION
  nullable    = false
}

# tflint-ignore: terraform_unused_declarations
variable "tags" {
  type        = map(string)
  default     = null
  description = "(Optional) Tags of the resource."
}
